plugins {
    id "java"
    id "maven-publish"
    id "com.github.johnrengelman.shadow" version "7.0.0"
}

var realLibs = [
        // ViaVersion -> https://github.com/ViaVersion/ViaVersion
        "com.viaversion:viaversion:4.5.0-22w44a-SNAPSHOT",

        // ViaBackwards -> https://github.com/ViaVersion/ViaBackwards
        "com.viaversion:viabackwards-common:4.5.0-22w44a-SNAPSHOT",

        // ViaRewind -> https://github.com/ViaVersion/ViaRewind
        "com.viaversion:viarewind-core:2.0.3-SNAPSHOT",

        // Snake YML -> https://github.com/snakeyaml/snakeyaml
        "org.yaml:snakeyaml:1.29",
]

var compileOnlyLibs = [
        // Google Common -> https://mvnrepository.com/artifact/com.google.guava/guava
        "com.google.guava:guava:31.1-jre",

        // Netty All -> https://mvnrepository.com/artifact/io.netty/netty-all
        "io.netty:netty-all:5.0.0.Alpha2",

        // Logger -> https://mvnrepository.com/artifact/org.apache.logging.log4j/log4j-core
        "org.apache.logging.log4j:log4j-core:2.19.0"
]

repositories {
    maven {
        url = "https://oss.sonatype.org/content/repositories/snapshots"
    }
    maven {
        url = "https://repo.viaversion.com"
    }
    maven {
        url = "https://jitpack.io"
    }
    mavenCentral()
}

dependencies {
    for (final def lib in realLibs)
        implementation(lib)

    for (final def lib in compileOnlyLibs)
        implementation(lib)
}

shadowJar {
    archiveClassifier.set("shadow")
    compileJava.options.encoding = "UTF-8"
    exclude "native-binaries/*"

    dependencies {
        for (final def lib in realLibs) {
            include(dependency(lib))
        }
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
        shadow(MavenPublication) { publication ->
            from project.shadow.component(publication)
            groupId = 'de.florianmichael'
            artifactId = 'viaprotocolhack'
            version = '22w42a'
        }
    }
}